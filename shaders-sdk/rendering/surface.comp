#version 460 core
#extension GL_GOOGLE_include_directive : enable

#ifdef WORK_SIZE
#undef WORK_SIZE
#endif
#define WORK_SIZE 512

#define USE_SINGLE_THREAD_RAY_MANAGMENT
#define SIMPLIFIED_RAY_MANAGMENT
#define DISCARD_SHARED_CACHING // store rays in registers if possible

#include "../include/constants.glsl"
#include "../include/structs.glsl"
#include "../include/uniforms.glsl"
#include "../include/rayslib.glsl"
#include "../include/random.glsl"
#include "../include/mathlib.glsl"
#include "../include/vertex.glsl"

struct Material {
     vec4 diffuse;
     vec4 specular;
     vec4 transmission;
     vec4 emissive;

     float ior;
     float roughness;
     float alpharef;
     float unk0f;

      int diffusePart;
      int specularPart;
      int bumpPart;
      int emissivePart;

     int flags;
     int alphafunc;
     int binding;
     int bitfield;

     ivec4 iModifiers0;
};

const uint MAX_TEXTURES = 72, MAX_SAMPLERS = 16;

// material set (in main descriptor set)
layout ( std430, binding = 2, set = 2 ) readonly buffer MaterialsSSBO {Material submats[]; };

// textrue/sampler set
layout ( binding = 0, set = 2 ) uniform texture2D textures[MAX_TEXTURES];
layout ( binding = 1, set = 2 ) uniform sampler samplers[MAX_SAMPLERS];



bool haveProp(in Material material, in int prop) {
    return (material.flags & prop) > 0;
}

bool haveProp(in int flags, in int prop) {
    return (flags & prop) > 0;
}

bool validateTexture(in int binding) {
    int _binding = binding-1;
    return binding > 0 && binding != LONGEST && binding <= MAX_TEXTURES && textureSize(sampler2D(textures[_binding], samplers[0]), 0).x > 0;
}

vec4 fetchTexture(in int binding, in vec2 texcoord) {
    int _binding = binding-1;
    vec4 result = texture(sampler2D(textures[_binding], samplers[0]), texcoord);
    result = mix(result, vec4(0.f), or(isnan(result), isinf(result)));
    return result;
}

vec4 fetchTexture(in int binding, in vec2 texcoord, in ivec2 offset) {
    int _binding = binding-1;
    vec4 result = texture(sampler2D(textures[_binding], samplers[0]), texcoord + vec2(offset) / textureSize(sampler2D(textures[_binding], samplers[0]), 0));
    result = mix(result, vec4(0.f), or(isnan(result), isinf(result)));
    return result;
}

vec4 fetchSpecular(in Material mat, in vec2 texcoord) {
    vec4 specular = mat.specular;
    if (validateTexture(mat.specularPart)) {
        specular = fetchTexture(mat.specularPart, texcoord);
    }
    return specular;
}

vec4 fetchEmissive(in Material mat, in vec2 texcoord) {
    vec4 emission = vec4(0.0f);
    if (validateTexture(mat.emissivePart)) {
        emission = fetchTexture(mat.emissivePart, texcoord);
    }
    return emission;
}

vec4 fetchTransmission(in Material mat, in vec2 texcoord) {
    return mat.transmission;
}

vec4 fetchNormal(in Material mat, in vec2 texcoord) {
    vec4 nmap = vec4(0.5f, 0.5f, 1.0f, 1.0f);
    if (validateTexture(mat.bumpPart)) {
        nmap = fetchTexture(mat.bumpPart, vec2(texcoord.x, texcoord.y));
    }
    return nmap;
}

vec4 fetchNormal(in Material mat, in vec2 texcoord, in ivec2 offset) {
    vec4 nmap = vec4(0.5f, 0.5f, 1.0f, 1.0f);
    if (validateTexture(mat.bumpPart)) {
        nmap = fetchTexture(mat.bumpPart, vec2(texcoord.x, texcoord.y), offset);
    }
    return nmap;
}

vec3 getNormalMapping(in int binding, vec2 texcoordi) {
    vec3 tc = fetchTexture(binding, texcoordi).xyz;
    vec3 normal = vec3(0.0f, 0.0f, 1.0f);
    IF (equalF(tc.x, tc.y) & equalF(tc.x, tc.z)) {
        const ivec2 off = ivec2(0,1);
        const float size = 1.0f;
        const float pike = 2.0f;
        vec3 p00 = vec3(0.0f, 0.0f, fetchTexture(binding, texcoordi, off.xx).x * pike);
        vec3 p01 = vec3(size, 0.0f, fetchTexture(binding, texcoordi, off.yx).x * pike);
        vec3 p10 = vec3(0.0f, size, fetchTexture(binding, texcoordi, off.xy).x * pike);
        normal = normalize(cross(p01 - p00, p10 - p00));
    } else {
        normal = normalize(fmix(vec3(0.0f, 0.0f, 1.0f), fma(tc, vec3(2.0f), vec3(-1.0f)), vec3(1.0f)));
    }
    return normal;
}

vec4 fetchDiffuse(in Material mat, in vec2 texcoord) {
    vec4 result = max(vec4(mat.diffuse.xyz, 1.f), vec4(0.f));
    if (validateTexture(mat.diffusePart)) {
        result = fetchTexture(mat.diffusePart, texcoord);
    }
    return result;
}



struct Textured {
    vec2 texcoord;

    int diffusePart;
    int specularPart;
    int bumpPart;
    int emissivePart;

    vec4 diffuse;
    vec4 specular;
    vec4 bump;
    vec4 emissive;
};

Textured textured;

#define material submats[matID]
//#define hit hits[globalID]
#define ray rayBlockNodes[hit.ray / R_BLOCK_SIZE][hit.ray % R_BLOCK_SIZE]

LOCAL_SIZE_LAYOUT;


HitRework interpolateMeshData(inout HitRework res) {
    int tri = floatBitsToInt(res.uvt.w);
    BOOL_ validInterpolant = greaterEqualF(res.uvt.z, 0.0f) & lessF(res.uvt.z, INFINITY) & BOOL_(tri != LONGEST);
    IFANY (validInterpolant) {
        ivec2 triv = gatherMosaic(getUniformCoord(tri));

        // transposed
        mat3x3 triverts = mat3x3(fetchMosaic(vertex_texture, triv, 0).xyz, fetchMosaic(vertex_texture, triv, 1).xyz, fetchMosaic(vertex_texture, triv, 2).xyz);
        vec3 deltaPos1 = triverts[1] - triverts[0];
        vec3 deltaPos2 = triverts[2] - triverts[0];
        vec3 normal = normalize(cross(deltaPos1, deltaPos2));

        // gather texcoord
        vec3 vs = vec3(1.0f - res.uvt.x - res.uvt.y, res.uvt.xy);
        vec2 sz = 1.f / textureSize(vertex_texture, 0);
        vec2 trig = fma(vec2(triv), sz, sz * 0.9999f);

        // gather 
#define trinorms triverts
        trinorms = mat3x3(SGATHER(normal_texture, trig, 0)._SWIZV, SGATHER(normal_texture, trig, 1)._SWIZV, SGATHER(normal_texture, trig, 2)._SWIZV);
        normal = faceforward(vs * trinorms, -normal, normal); // mult matrix

        mat2x3 texcoords = mat2x3(SGATHER(texcoords_texture, trig, 0)._SWIZV, SGATHER(texcoords_texture, trig, 1)._SWIZV);
        mat3x2 txds = transpose(texcoords);
        mat2x2 dlts = mat2x2(txds[1]-txds[0],txds[2]-txds[0]);

        // 
        if (abs(dlts[0].x) < 0.00001f && abs(dlts[0].y) < 0.00001f) dlts[0].x = -1.f, dlts[0].y = 0.f; // correction txcoords delta x
        if (abs(dlts[1].x) < 0.00001f && abs(dlts[1].y) < 0.00001f) dlts[1].x =  0.f, dlts[1].y = 1.f; // correction txcoords delta y

        // tangent determinant
        float det = determinant(dlts), f = 1.f / (max(abs(det), 0.00001f) * (det >= 0.f ? 1 : -1));
        { f = isnan(f) ? 0.f : f, f = isinf(f) ? 10000.f : f; }
        vec3 tang = fma(dlts[1].yyy, deltaPos1, -dlts[0].y * deltaPos2) * f;

        IF (validInterpolant) {
            res.normalHeight = vec4(normal, 0.0f);
            res.tangent = vec4(normalize(tang - normal * sign(dot(tang, normal))), 0.0f);
            res.texcoord.xy = vs * texcoords; // mult matrix
            res.materialID = materials[tri];
            HitActived(res, TRUE_); // temporary enable
        }
    }
    return res;
}


void main() {
    #define wsize RAY_BLOCK samplerUniform.hitCount
    //int wsize = RAY_BLOCK samplerUniform.hitCount;
    int wcount = min(tiled(wsize, int(gl_WorkGroupSize.x)), 50000);
    bool overflow = false;
    int wgs = int(gl_NumWorkGroups);
    for (int w=0;w<wcount;w+=wgs) {
        int globalID = (w + int(gl_WorkGroupID.x)) * int(gl_WorkGroupSize.x) + int(gl_LocalInvocationID.x);
        overflow = overflow || globalID >= RAY_BLOCK samplerUniform.hitCount || globalID < 0;
        
        IFALL(overflow) break;
        {
            HitRework hit = hits[globalID];
            interpolateMeshData(hit);

            globalInvocationSMP = globalID;

            int matID = hit.materialID - RAY_BLOCK materialUniform.materialOffset;
            overflow = overflow || (matID >= RAY_BLOCK materialUniform.materialCount || matID < 0);

            vec2 texcoord = hit.texcoord.xy;
            textured.texcoord = texcoord;

            // texture indexes
            textured.diffusePart = material.diffusePart;
            textured.bumpPart = material.bumpPart;
            textured.specularPart = material.specularPart;
            textured.emissivePart = material.emissivePart;

            // static colors
            textured.diffuse = material.diffuse;
            textured.bump = vec4(0.f, 0.f, 1.f, 1.f);
            textured.specular = material.specular;
            textured.emissive = material.emissive;

            // AMD not supported per-lane texture data loading, if texture index is not constant
#ifdef ENABLE_AMD_INSTRUCTION_SET
            bool wasTextured = false;
            for (int wl=0;wl<WARP_SIZE_RT;wl++) {
                IFALL(wasTextured) break;
                if (!wasTextured && readLane(hit.materialID, wl) == hit.materialID) { // don't occupy warpy
                    if (validateTexture(textured.bumpPart)) textured.bump.xyz = getNormalMapping(textured.bumpPart, textured.texcoord);
                    if (validateTexture(textured.diffusePart)) textured.diffuse = fetchTexture(textured.diffusePart, textured.texcoord);
                    if (validateTexture(textured.specularPart)) textured.specular = fetchTexture(textured.specularPart, textured.texcoord);
                    if (validateTexture(textured.emissivePart)) textured.emissive = fetchTexture(textured.emissivePart, textured.texcoord);
                    wasTextured = true;
                }
            }
#else
            if (validateTexture(textured.diffusePart)) textured.diffuse = fetchTexture(textured.diffusePart, textured.texcoord);
            if (validateTexture(textured.specularPart)) textured.specular = fetchTexture(textured.specularPart, textured.texcoord);
            if (validateTexture(textured.emissivePart)) textured.emissive = fetchTexture(textured.emissivePart, textured.texcoord);
            if (validateTexture(textured.bumpPart)) textured.bump.xyz = getNormalMapping(textured.bumpPart, textured.texcoord);
#endif

            vec3 normal = normalize(hit.normalHeight.xyz);
            vec3 tangent = normalize(hit.tangent.xyz);
            mat3 tbn = mat3(tangent.xyz, normalize(cross(normal.xyz, tangent.xyz)), normal.xyz);

            vec4 diffuse = textured.diffuse;
            vec4 emission = textured.emissive*1.25f;
            vec4 spc = clamp01(textured.specular);

            emission.xyz = toLinear(max(emission.xyz, vec3(0.f)));
            diffuse.xyz = toLinear(max(diffuse.xyz, vec3(0.f)));
            //diffuse = vec4(1.f.xxxx);

            hit.normalHeight.xyzw = vec4(normalize(tbn*textured.bump.xyz), 1.f).xyzw;
            hit.emission.xy = packHalf2(emission.xyzw).xy;
            hit.albedo.xy = packHalf2(diffuse.xyzw).xy;
            hit.metallicRoughness.xy = packHalf2(vec4(spc.yz, 0.f, 0.f).xyzw).xy;
            HitActived(hit, TRUE_);

            if (!overflow) hits[globalID] = hit;
        }
    }
}
