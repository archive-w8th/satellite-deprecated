#version 460 core

#define VERTEX_FILLING

#include "../include/constants.glsl"
#include "../include/structs.glsl"
//#include "../include/uniforms.glsl"
#include "../include/vertex.glsl"
#include "../include/mathlib.glsl"
#include "../include/ballotlib.glsl"

LOCAL_SIZE_LINEAR_LAYOUT;

layout ( std430, binding = 0, set = 0 ) restrict buffer BuildCounters {
    int cCounter;
    int lCounter;
    int aCounter;
    int bvhLevel;
    int aRange[2];
    int leafCounter[1];
    int tcounter[1];
};

// input data
layout ( std430, binding = 1, set = 0 ) readonly buffer InputVertexSSBO {float iverts[]; };
layout ( std430, binding = 2, set = 0 ) readonly buffer InputVertexIndicesSSBO {INDICE_T vindics[]; };
//layout ( std430, binding = 3, set = 0 ) readonly buffer InputMaterialsSSBO {int imats[]; };

// mesh attributes and accessors
layout ( std430, binding = 6, set = 0 ) readonly buffer IMeshUniform {MeshUniformStruct meshUniform; };
layout ( std430, binding = 8, set = 0 ) readonly buffer IBufferViews {VirtualBufferView bufferViews[]; };
layout ( std430, binding = 7, set = 0 ) readonly buffer IMeshAccessors {VirtualAccessor meshAccessors[]; };

// output geometry and counters
//layout ( std430, binding = 0, set = 0 ) restrict buffer TCounterSSBO {int tcounter[]; };

// tools
vec4 readByAccessor(in uint accessorID, in uint idx) {
    VirtualAccessor accessors = meshAccessors[accessorID];
    VirtualBufferView bufferView = bufferViews[accessors.bufferView];
    uint cmps = aComponents(accessors);
    uint stride4 = bufferView.stride4 > 0 ? bufferView.stride4 : (cmps+1);

    // get buffer view
    uint generalOffset = idx * stride4 + bufferView.offset4;

    // get accessor structure offset
    uint componentOffset = generalOffset + accessors.offset4;

    // get components
    vec4 data = vec4(
        cmps >= 0 ? iverts[componentOffset + 0] : 0,
        cmps >= 1 ? iverts[componentOffset + 1] : 0,
        cmps >= 2 ? iverts[componentOffset + 2] : 0,
        cmps >= 3 ? iverts[componentOffset + 3] : 0
    );

    // return data
    return data;
}


const int m[3] = {3, 0, 2};
const int ms[3] = {2, 1, 3};

initAtomicIncByFunction(tcounter[0], tcounterInc, int)


void main()
{
    if (gl_GlobalInvocationID.x >= meshUniform.nodeCount) return;

    const int ctriangle = int(gl_GlobalInvocationID.x);
    const int matID = int(meshUniform.materialID);
    vec3 vertice[4] = {vec3(0.0f), vec3(0.0f), vec3(0.0f), vec3(0.0f)};
    vec3 normal[4] = {vec3(0.0f), vec3(0.0f), vec3(0.0f), vec3(0.0f)};
    vec4 texcoord[4] = {vec4(0.0f), vec4(0.0f), vec4(0.0f), vec4(0.0f)};
    vec4 colors[4] = {vec4(0.0f), vec4(0.0f), vec4(0.0f), vec4(0.0f)};
    vec4 mods[4] = {vec4(0.0f), vec4(0.0f), vec4(0.0f), vec4(0.0f)};

    const int mode = meshUniform.primitiveType;
    const int trp = mode == 1 ? 4 : 3;
    for (int i=0;i<trp;i++) {
        uint ctri = ctriangle * trp + i;
        //uint ctri = (meshUniform.loadingOffset + ctriangle) * trp + i;
        uint ptri = meshUniform.loadingOffset + ctri;
        //uint ptri = ctri;
        uint vindices = meshUniform.isIndexed == 0 ? ptri : uint( PICK(vindics, ptri) );

        vec3 vpos = vec3(0.0f);
        vec4 vtex = vec4(0.0f);
        vec3 vnorm = vec3(0.0f);
        vec4 modif = vec4(0.0f);
        vec4 color = vec4(1.0f);

        { // vertex accessor are required
            vpos.xyz = readByAccessor(meshUniform.vertexAccessor, vindices).xyz;
        }
        if (meshUniform.normalAccessor != -1) {
            vnorm.xyz = readByAccessor(meshUniform.normalAccessor, vindices).xyz;
        }
        if (meshUniform.modifierAccessor != -1) {
            modif = readByAccessor(meshUniform.modifierAccessor, vindices);
        }
        if (meshUniform.texcoordAccessor != -1) {
            vtex.xy = readByAccessor(meshUniform.texcoordAccessor, vindices).xy;
        }
#ifdef INVERT_TX_Y
        vtex.y = 1.0f - vtex.y; // invert Y
#endif

        vec4 normalf  = mult4(meshUniform.transformInv, vec4(vnorm, 0.0f));
        vec4 verticef = mult4(meshUniform.transform, vec4(vpos , 1.0f));
        //vec4 verticef = vec4(vpos , 1.0f);
        verticef.xyz /= verticef.w;

        vertice[i] = verticef.xyz;
        normal[i] = normalf.xyz;
        texcoord[i] = vtex;
        colors[i] = color;
        mods[i] = modif;
    }

    // adapt quads to triangles
    const int istride = mode == 1 ? 2 : 1;
    int tidc = meshUniform.storingOffset + int(gl_GlobalInvocationID.x) * istride;
    tcounterInc(TRUE_, istride);

    vec3 offsetnormal = normalize(cross(vertice[1] - vertice[0], vertice[2] - vertice[0]));
    const ivec2 isize = ivec2(imageSize(vertex_texture_out));

    {
        materials[tidc] = matID;
        for (int i=0;i<3;i++) {
            ivec2 mosaicCoord = mosaicIdc(gatherMosaic(getUniformCoord(tidc)), i);
            mosaicCoord.x = mosaicCoord.x % isize.x;
            //mosaicCoord.y = mosaicCoord.y % isize.y; // may have risks
            if (mlength(abs(normal[i])) >= 0.0001f && meshUniform.normalAccessor != -1) {
                ISTORE(normal_texture_out, mosaicCoord, vec4(normalize(normal[i]), 1.0f));
            } else {
                ISTORE(normal_texture_out, mosaicCoord, vec4(normalize(offsetnormal.xyz), 1.0f));
            }
            ISTORE(vertex_texture_out, mosaicCoord, vec4(vertice[i], 1.0f));
            ISTORE(texcoords_texture_out, mosaicCoord, vec4(texcoord[i]));
            ISTORE(modifiers_texture_out, mosaicCoord, vec4(mods[i]));
        }
    }

    if (mode == 1) {
        tidc += 1;
        materials[tidc] = matID;
        for (int i=0;i<3;i++) {
            //int mi = m[i];
            #define mi m[i]
            ivec2 mosaicCoord = mosaicIdc(gatherMosaic(getUniformCoord(tidc)), i);
            mosaicCoord.x = mosaicCoord.x % isize.x;
            //mosaicCoord.y = mosaicCoord.y % isize.y; // may have risks
            if (mlength(abs(normal[mi])) >= 0.0001f && meshUniform.normalAccessor != -1) {
                ISTORE(normal_texture_out, mosaicCoord, vec4(normalize(normal[mi]), 1.0f));
            } else {
                ISTORE(normal_texture_out, mosaicCoord, vec4(normalize(offsetnormal.xyz), 1.0f));
            }
            ISTORE(vertex_texture_out, mosaicCoord, vec4(vertice[mi], 1.0f));
            ISTORE(texcoords_texture_out, mosaicCoord, vec4(texcoord[mi]));
            ISTORE(modifiers_texture_out, mosaicCoord, vec4(mods[mi]));
        }
    }
}
