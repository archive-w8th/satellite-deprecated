#version 460 core

#define BVH_CREATION

#include "../include/constants.glsl"
#include "../include/structs.glsl"
#include "../include/uniforms.glsl"
#include "../include/mathlib.glsl"
#include "../include/ballotlib.glsl"
#include "../include/vertex.glsl"
#include "./includes.glsl"

LOCAL_SIZE_LAYOUT;


int findSplit( in int first, in int last) {
    MORTONTYPE firstCode = Mortoncodes[first], lastCode = Mortoncodes[last];
    int commonPrefix = nlz(firstCode ^ lastCode), split = commonPrefix == 0 ? ((first + last) >> 1) : first, step = last - first;
    for (int i=0;(i<8192 && step>1 && commonPrefix != 0);i++) {
        step = (step + 1) >> 1;
        int newSplit = split + step;
        split = newSplit < last && nlz(firstCode ^ Mortoncodes[newSplit]) > commonPrefix ? newSplit : split;
    }
    return clamp(split, first, last-1);
}

void resetBox(inout HlbvhNode node){
    node.lrbox = PACK_BOX_(UBOXF_UNPACKED_(1000.f.xxxx, 1000.f.xxxx, -1000.f.xxxx, -1000.f.xxxx));
}

initAtomicIncByFunction(lCounter, lCounterInc, int)
initAtomicIncFunction(aCounter, aCounterInc, int)
initAtomicIncFunction(cCounter, cCounterInc, int)

void splitNode(in int trID) {
    int prID = exchange(Actives[trID], -1);

    //HlbvhNode parentNode = Nodes[prID];
    ivec2 _hidx = bvhLinear2D(prID);
    ivec2 _pdata = ivec2(
        imageLoad(bvhStorage, _hidx * ivec2(4,1) + ivec2(0,0)).x,
        imageLoad(bvhStorage, _hidx * ivec2(4,1) + ivec2(1,0)).x
    );

    if (_pdata.x != _pdata.y && prID != -1) {
        // find split
        int split = findSplit(_pdata.x, _pdata.y);
        int hid = lCounterInc(TRUE_, 2);
        
        // encode with SIMD-like
        ivec4 transplit = ivec4(_pdata.x, split+0, split+1, _pdata.y);
        bvec2 isLeaf = lessThan(transplit.yw - transplit.xz, ivec2(1));
        ivec2 hd = hid + ivec2(0, 1);

        // left child
        if (isLeaf.x) {
            exchange(LeafIndices[cCounterInc(TRUE_)], hd.x);
        } else { // add to next works
            exchange(Actives[aCounterInc(TRUE_)], hd.x);
        }

        // right child
        if (isLeaf.y) {
            exchange(LeafIndices[cCounterInc(TRUE_)], hd.y);
        } else { // add to next works
            exchange(Actives[aCounterInc(TRUE_)], hd.y);
        }

        ivec2 _lidx = bvhLinear2D(hd.x), _ridx = bvhLinear2D(hd.y);

        Flags[hd.x] = 0;
        imageStore(bvhStorage, _lidx * ivec2(4,1) + ivec2(0,0), transplit.xxxx);
        imageStore(bvhStorage, _lidx * ivec2(4,1) + ivec2(1,0), transplit.yyyy);
        imageStore(bvhStorage, _lidx * ivec2(4,1) + ivec2(2,0), prID.xxxx);
        imageStore(bvhStorage, _lidx * ivec2(4,1) + ivec2(3,0), (-1).xxxx);
        imageStore(bvhBoxes, _lidx * ivec2(2,1) + ivec2(0,0),  UBLANEF_UNPACKED_(1000.f).xxxx);
        imageStore(bvhBoxes, _lidx * ivec2(2,1) + ivec2(1,0), -UBLANEF_UNPACKED_(1000.f).xxxx);
        
        Flags[hd.y] = 0;
        imageStore(bvhStorage, _ridx * ivec2(4,1) + ivec2(0,0), transplit.zzzz);
        imageStore(bvhStorage, _ridx * ivec2(4,1) + ivec2(1,0), transplit.wwww);
        imageStore(bvhStorage, _ridx * ivec2(4,1) + ivec2(2,0), prID.xxxx);
        imageStore(bvhStorage, _ridx * ivec2(4,1) + ivec2(3,0), (-1).xxxx);
        imageStore(bvhBoxes, _ridx * ivec2(2,1) + ivec2(0,0),  UBLANEF_UNPACKED_(1000.f).xxxx);
        imageStore(bvhBoxes, _ridx * ivec2(2,1) + ivec2(1,0), -UBLANEF_UNPACKED_(1000.f).xxxx);

        // link parent with childs
        imageStore(bvhStorage, _hidx * ivec2(4,1) + ivec2(0,0), hd.xxxx);
        imageStore(bvhStorage, _hidx * ivec2(4,1) + ivec2(1,0), hd.yyyy);
    }
}

void main() {
    uint threadID = uint(gl_LocalInvocationID.x);
    int workID = int(gl_WorkGroupID.x);
    int WS = int(gl_WorkGroupSize.x);
    int hWS = int(gl_WorkGroupSize.x) / 2;

    // create initial (root) node
    bool isBeginning = aRange[1] == 0;
    if (gl_GlobalInvocationID.x == 0 && isBeginning) {
        int hid = lCounterInc(TRUE_, 2);
        exchange(Actives[aCounterInc(TRUE_)], hid);

        //HlbvhNode node = Nodes[hid]; resetBox(node);
        //node.pdata = ivec4(0, aabbcount[0]-1, (-1).xx);
        
        ivec2 _hidx = bvhLinear2D(hid);
        imageStore(bvhStorage, _hidx * ivec2(4,1) + ivec2(0,0), (0).xxxx);
        imageStore(bvhStorage, _hidx * ivec2(4,1) + ivec2(1,0), (aabbcount[0]-1).xxxx);
        imageStore(bvhStorage, _hidx * ivec2(4,1) + ivec2(2,0), (-1).xxxx);
        imageStore(bvhStorage, _hidx * ivec2(4,1) + ivec2(3,0), (-1).xxxx);

        //Nodes[hid] = node;
        Flags[hid] = 0;
    }

    memoryBarrier(); barrier(); 

    // begin work on splitting
    int ranged[2] = (isBeginning && workID == 0) ? int[2](0, 1) : int[2](aRange[0], aRange[1]);
    uint workSize = min((ranged[1] <= ranged[0]) ? 0 : (((ranged[1] - ranged[0]) - 1) / WS + 1), 8192);
    int wgs = int(gl_NumWorkGroups);
    for (int i=0;i<workSize;i+=wgs) {
        int trID = ranged[0] + WS * (i+workID) + int(threadID);
        if (trID >= ranged[1]) break;
        if (trID < ranged[1]) splitNode(trID);
    }
}
